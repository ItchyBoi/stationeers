# remember, this programm only works if
# the firstpart ist already exported!!
# credit goes to TryCatch
alias Dial d0
alias Lever d1
alias LEDFurnace d2
alias Furnace d3
alias coolvalve d4
alias fuelvalve d5
move r7 -1
s Dial Mode 9
s coolvalve On 1
s fuelvalve On 1
s Furnace SettingInput 100
main: # initial state
move r10 0 # reset fuel throughput
s Furnace SettingOutput 0
s coolvalve Setting 0
s fuelvalve Setting 0
idle:
yield
jal getState # load parameters
l r0 Lever Open
s Furnace Open 0
s Furnace On r0
s LEDFurnace On r0
beqz r0 idle
smelting:
s Furnace Activate 1
l r9 Furnace Pressure
l r8 Furnace Temperature
slt r2 r9 r15 # is press to low?
slt r3 r8 r13 # is temp to low?
or r2 r2 r3 # is press or temp lower than need?..
move r6 0
bleal r9 r15 addfuel # is press to low?
bleal r8 r13 addfuel # is temp to low?
s fuelvalve Setting r6
sgt r0 r9 r14 # is press to high?
sge r1 r8 r12 # is temp to high?
and r5 r0 r3 # is press to high & tamp to low?
bgtal r5 0 flushsystem
move r6 0
bgeal r9 r14 dumpwaste # is press to high?
s Furnace SettingOutput r6
move r6 0
bgeal r8 r12 coolfurnace # is temp to high?
s coolvalve Setting r6
or r0 r0 r1 # is press or temp higher than need?
nor r4 r0 r2 # is both press and temp are suitable?
select r4 r4 2 4 # green when "ready" else red
ls r0 Furnace 0 Occupied
select r4 r0 5 r4 # yellow while furnace melts
s LEDFurnace Color r4
l r4 Furnace RecipeHash
jal getIngotHash
seq r4 r4 r11 # check if furnace contains alloy
s Furnace Open r4
l r0 Lever Open # shutdown when Lever "off"
nor r1 r0 0
or r4 r4 r1 # also eject if going to shutdown
s Furnace Open r4
beq r0 0 main # exit because of Lever "off"
l r4 Dial Setting # reload state on Dial change
bne r4 r7 idle
yield
j smelting
getState:
l r0 Dial Setting
beq r0 r7 getIngotHash # compare with old dial value
move r7 r0
mul sp r7 10 # offset cursor for the record
add sp sp 10 # cursor to beginning of the record
pop r12 # max temperature
pop r13 # min temperature
pop r14 # max pressure
pop r15 # min pressure
getIngotHash:
pop r11 # ingot hash
s db Setting r11 # show recipe
bnez r11 ra
move sp 6  # fix Stuck After Game Restarts
peek r11   # bug in stack implementation
beqz r11 0 # I wish devs will fix it someday...
mul sp r7 10
add sp sp 6
j getIngotHash
addfuel:
move r6 30
j ra
dumpwaste:
move r6 20
j ra
coolfurnace:
move r6 100
j ra
flushsystem:
s LEDFurnace Color 4
s coolvalve Setting 0
s fuelvalve Setting 0
s Furnace SettingOutput 100
flushing:
l r9 Furnace Pressure
yield
blt r9 5 ra
j flushing